(define (make-account balance password)
  (define (make pw)
    (define (withdraw amount)
      (if (>= balance amount)
          (begin (set! balance (- balance amount))
                 balance)
          "Insufficient funds"))
    (define (deposit amount)
      (set! balance (+ balance amount))
      balance)
    (define (join other-password)
      (make other-password))
    (define (dispatch p m)
      (if (eq? p pw)
          (cond ((eq? m 'withdraw) withdraw)
                ((eq? m 'deposit) deposit)
                ((eq? m 'join) join)
                (else (error "Unknow request -- MAKE-ACCOUNT"
                             m)))
          (lambda (x) "Insufficient password")))
    dispatch)
  (make password))

(define (make-join account password other-password)
  ((account password 'join) other-password))

(define peter-acc (make-account 100 'open-sesame))
((peter-acc 'open-sesame 'withdraw) 10)
(define paul-acc (make-join peter-acc 'open-sesame 'rosebud))
((paul-acc 'rosebud 'deposit) 10)
((peter-acc 'open-sesame 'deposit) 20)
